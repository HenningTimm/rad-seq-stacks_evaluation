workflows = ["no_dedup", "with_dedup"]

configfile: "config.yaml"

rule all:
    input:
        expand(
            "rad-seq-stacks_{workflow}/plots/distribution_comparison/stacks_counts.pdf",
            workflow=workflows,
        ),
        expand(
            "rad-seq-stacks_{workflow}/plots/distribution_comparison/stacks_size_distribution.pdf",
            workflow=workflows,
        ),
        "compared_stack_sizes.pdf",

rule run_subworkflow:
    input:
        "rad-seq-stacks_{workflow}/individuals.tsv",
        "rad-seq-stacks_{workflow}/units.tsv",
    output:
        "rad-seq-stacks_{workflow}/plots/distribution_comparison/stacks_counts.pdf",
        "rad-seq-stacks_{workflow}/plots/distribution_comparison/stacks_size_distribution.pdf",
        "rad-seq-stacks_{workflow}/plots/distribution_comparison/sizes_dataframe.csv",
        "rad-seq-stacks_{workflow}/plots/distribution_comparison/counts_dataframe.csv",
    params:
        cores=config["cores_per_subworkflow"]
    shell:
        "cd rad-seq-stacks_{wildcards.workflow} && "
        "snakemake --use-conda --jobs {params.cores} -F"

rule plot_comparison:
    input:
        sizes_no_dedup="rad-seq-stacks_no_dedup/plots/distribution_comparison/sizes_dataframe.csv",
        sizes_with_dedup="rad-seq-stacks_with_dedup/plots/distribution_comparison/sizes_dataframe.csv",
        counts_no_dedup="rad-seq-stacks_no_dedup/plots/distribution_comparison/counts_dataframe.csv",
        counts_with_dedup="rad-seq-stacks_with_dedup/plots/distribution_comparison/counts_dataframe.csv",
    output:
        violin_path="compared_stack_sizes.pdf",
        scatter_path="compared_stack_counts.pdf",
    params:
        # threshold=60
        threshold=config["plotting_threshold"]
    conda:
        "rad-seq-stacks_with_dedup/envs/plot_stacks_dist.yaml"
    script:
        "scripts/compare_pipelines.py"

rule rage_dataset:
    output:
        "testdata/testdata_1.fastq.gz",
        "testdata/testdata_2.fastq.gz",
        "testdata/testdata_gt.yaml",
        "testdata/testdata_barcodes.txt",
    conda:
        "rad-seq-stacks_with_dedup/envs/testdata.yaml"
    params:
        loci=config["ddrage"]["loci"],
        individuals=config["ddrage"]["individuals"],
        output_prefix=config["ddrage"]["output_prefix"],
        ds_name=config["ddrage"]["ds_name"],
        hrl_number=config["ddrage"]["hrl_number"],
        prob_ID=config["ddrage"]["prob_ID"],
        prob_het=config["ddrage"]["prob_het"],
        event_probs=config["ddrage"]["event_probs"],
        prob_seq_error=config["ddrage"]["prob_seq_error"],
        expected_coverage=config["ddrage"]["expected_coverage"],
        bbd_alpha=config["ddrage"]["bbd_alpha"],
        bbd_beta=config["ddrage"]["bbd_beta"],
    shell:
        "ddrage --name {params.ds_name} -l {params.loci} -n {params.individuals} -o {params.output_prefix} "
        "--hrl-number {params.hrl_number} --prob-incomplete-digestion {params.prob_ID} "
        "--prob-heterozygous {params.prob_het} --event-probabilities {params.event_probs} "
        "--coverage {params.expected_coverage} --BBD-alpha {params.bbd_alpha} --BBD-beta {params.bbd_beta} "
        "-z --prob-seq-error {params.prob_seq_error} && "
        "mv {params.output_prefix}/logs/* {params.output_prefix}/ && "
        "rename -f 's/[ACGT]{{6}}_//' {params.output_prefix}/* && "
        "mv {params.output_prefix}/* testdata/ && "
        "rm -r {params.output_prefix}"


rule remove_annotation:
    input:
        fq1="testdata/testdata_1.fastq.gz",
        fq2="testdata/testdata_2.fastq.gz",
    output:
        fq1="testdata/testdata_1_noheader.fastq.gz",
        fq2="testdata/testdata_2_noheader.fastq.gz",
        gt1="testdata/testdata_1_annotation.txt",
        gt2="testdata/testdata_2_annotation.txt",
    conda:
        "rad-seq-stacks_with_dedup/envs/testdata.yaml"
    shell:
        "remove_annotation {input.fq1} {input.fq2}"


rule units_and_individuals:
    input:
        fq1="testdata/testdata_1_noheader.fastq.gz",
        fq2="testdata/testdata_2_noheader.fastq.gz",
        bc_file="testdata/testdata_barcodes.txt",
    output:
        individuals="rad-seq-stacks_{pipeline_config}/individuals.tsv",
        units="rad-seq-stacks_{pipeline_config}/units.tsv",
    script:
        "scripts/generate_sample_files.py"

